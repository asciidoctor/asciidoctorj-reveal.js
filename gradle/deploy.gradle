apply plugin: 'com.jfrog.bintray'

bintray {
  user = System.env['BINTRAY_USER'] ?: (project.hasProperty('bintrayUsername') ? project.bintrayUsername : '')
  key = System.env['BINTRAY_KEY'] ?: (project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : '')

  publications = ['jars']

  if ( !project.hasProperty('skip.signing') ) {
    // Copy the signed pom to bintrayDestination
    filesSpec {
      from signPom
      into signPom.bintrayDestination
    }
    bintrayUpload.dependsOn signPom
  }

  dryRun = project.hasProperty('dryRun') && project.dryRun.toBoolean()
  publish = project.statusIsRelease

  pkg {
    repo = System.env['BINTRAY_REPO'] ?: (project.hasProperty('bintrayRepo') ? project.bintrayRepo : 'maven')
    if (!project.hasProperty('bintrayPersonal')) {
      userOrg = 'asciidoctor'
    }

    name = project.name
    desc = project.description

    // NOTE use this instead if we want to list all artifacts under the same package
    //name = project(':asciidoctorj').name
    //desc = project(':asciidoctorj').description

    licenses = ['Apache-2.0']
    labels = ['asciidoctor', 'asciidoctorj', 'asciidoc']
    websiteUrl = 'http://asciidoctor.org/docs/asciidoctorj-pdf'
    issueTrackerUrl = 'https://github.com/asciidoctor/asciidoctorj-pdf/issues'
    vcsUrl = 'https://github.com/asciidoctor/asciidoctorj-pdf'
    publicDownloadNumbers = true

    version {
      name = project.version
      // NOTE customize the vcsTag if you're only releasing a subproject
      vcsTag = "${rootProject.name}-${rootProject.version}"
      //released = "$buildDateOnly"
      // desc = ...
    }
  }
}

bintrayUpload.dependsOn build
